{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "execution": {
     "iopub.status.busy": "2020-08-09T11:26:46Z",
     "iopub.status.idle": "2020-08-09T11:26:46Z"
    }
   },
   "source": [
    "# 简单版本的求值器"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "execution": {
     "iopub.status.busy": "2020-08-10T13:22:13Z",
     "iopub.status.idle": "2020-08-10T13:22:21Z"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ";;; M-Eval input:\n"
     ]
    },
    {
     "name": "stdin",
     "output_type": "stream",
     "text": [
      " \n",
      " s\n"
     ]
    },
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "Unbound variable s\n"
     ]
    }
   ],
   "source": [
    "(require sicp)\n",
    "; 如下做一个简单的scheme求值,\n",
    "; 求值器的内核。\n",
    "; 分成两大部分，一个是eval，一个是apply\n",
    "; 简单点，就是eval规约为apply，而apply中有些表达式又需要eval来处理。\n",
    "\n",
    "(define (eval exp env)\n",
    "  (cond ((self-evaluating? exp) exp)                      ; 自求值，数字或者字符串\n",
    "        ((variable? exp) (lookup-variable-value exp env)) ; 在环境中找出变量\n",
    "        ((quoted? exp) (text-of-quotation exp))           ; '\n",
    "        ((assignment? exp) (eval-assignment exp env))     ; 变量的赋值（定义）。\n",
    "        ((definition? exp) (eval-definition exp env))     ; 定义的。\n",
    "        ((if? exp) (eval-if exp env))                           ; if 语句\n",
    "        ((lambda? exp) (make-procedure (lambda-parameters exp)  ; lambda，转化为可以应用的过程\n",
    "                                       (lambda-body exp)\n",
    "                                       env))\n",
    "        ((begin? exp)                                    ; begin语句，求值一系列的表达式，按照它们出现的顺序。\n",
    "         (eval-sequence (begin-actions exp) env))\n",
    "        ((cond? exp) (eval (cond->if exp) env))          ; cond语句\n",
    "        ((application? exp)                              ; 函数过程。\n",
    "         (myapply (eval (operator exp) env)              ; 这里是生成函数名，但这个也是要递归处理的，函数名是可能由表达式生成的。 \n",
    "                (list-of-values (operands exp) env)))    ; 生成参数\n",
    "        (else\n",
    "         (error \"Unknown expression type: EVAL\" exp))))\n",
    "\n",
    "; 清注意： 原文中是apply，这个跟系统原先的apply冲突，毕竟我需要调用系统的apply，所以这里改名字了。\n",
    "; 当然，也可以先保存原先的引用 ; (define apply-in-underlying-scheme apply) ，然后再修改。不过我没测试过。\n",
    "; 我优先的方法是，尽量避免重复名字。\n",
    "(define (myapply procedure arguments)         ; 参数，一个是过程，一个是该过程应该去实际应用的表。\n",
    "  (cond ((primitive-procedure? procedure)                   ; 基本过程\n",
    "         (apply-primitive-procedure procedure arguments))   ; 只是简单的执行后边的东西。\n",
    "        ((compound-procedure? procedure)                    ; 复合过程，需要建立相应的环境。\n",
    "         (eval-sequence                         ; 执行一个序列，相当于begin吧。\n",
    "          (procedure-body procedure)            ; 一堆表达式\n",
    "          (extend-environment                   ; 创建一个新的环境。\n",
    "           (procedure-parameters procedure)     ; 变量名\n",
    "           arguments                            ; 变量值\n",
    "           (procedure-environment procedure)))) ; 上一层环境。\n",
    "        (else      ; 其他情况\n",
    "         (error    ; 就是错误啦\n",
    "          \"Unknown procedure type: APPLY\" procedure))))\n",
    "\n",
    "; 过程参数，\n",
    "; eval在处理过程引用时用 如下的生成实际的参数表\n",
    "(define (list-of-values exps env)\n",
    "  (if (no-operands? exps)                    ; 首先判断是否有参数\n",
    "      '()                                    ; 没参数返回空表\n",
    "      (cons (eval (first-operand exps) env)  ; 这里表示有参数,用eval来求出实际的值\n",
    "            (list-of-values (rest-operands exps) env))))  ; 递归处理其他的参数。\n",
    "; 条件\n",
    "; if条件的\n",
    "(define (eval-if exp env)\n",
    "  (if (true? (eval (if-predicate exp) env)) ; 求值if表达式,\n",
    "      (eval (if-consequent exp) env)        ; 如果为真,就执行这个\n",
    "      (eval (if-alternative exp) env)))     ; 这里表示为假就执行的。\n",
    "\n",
    "; 序列,一个表达式序列和一个环境为参数,\n",
    "(define (eval-sequence exps env)\n",
    "  (cond ((last-exp? exps)             ; 如果是最后一个表达式,\n",
    "         (eval (first-exp exps) env)) ; 求值这个表达式,返回结果。\n",
    "        (else                         ; 其他情况\n",
    "         (eval (first-exp exps) env)  ; 求值这个表达式,\n",
    "         (eval-sequence (rest-exps exps) env)))) ; 然后其他表达式递归处理\n",
    "\n",
    "; 赋值和定义\n",
    "(define (eval-assignment exp env)                   \n",
    "  (set-variable-value! (assignment-variable exp)            ; set-variable-value!将有关的变量和值安置到指定的环境里。\n",
    "                       (eval (assignment-value exp) env)    ; 计算需要赋的值\n",
    "                       env)                                 ; 在指定的环境。\n",
    "  'ok)\n",
    "; 定义过程类似\n",
    "(define (eval-definition exp env)\n",
    "  (define-variable! (definition-variable exp)\n",
    "    (eval (definition-value exp) env)\n",
    "    env)\n",
    "  'ok)\n",
    "\n",
    "; 表达式的表示,如下是几个语法规范\n",
    "(define (self-evaluating? exp)    ; 自求值表达式只有\n",
    "  (cond ((number? exp) true)      ; 数字\n",
    "        ((string? exp) true)      ; 字符串\n",
    "        (else false)))\n",
    "\n",
    "(define (tagged-list? exp tag)  ; 确定一个表的开始是不是某个给定的符号。\n",
    "  (if (pair? exp)               ; 这个是括号表达式\n",
    "      (eq? (car exp) tag)       ; 第一项目是tag吗?,比如exp是(set! <var> <value>),那么这个是比较set!是否跟参数中的tag是一样的。\n",
    "      false))\n",
    "\n",
    "(define (variable? exp) (symbol? exp))  ; 是否变量\n",
    "(define (quoted? exp) (tagged-list? exp 'quote))  ; 引号表达式的形式是(quete <text-of-quotation>) ,'a 实际上是(quote a)\n",
    "(define (text-of-quotation exp) (cadr exp))       ; 引用表达式是(quete a b c) = ('a b c)，所以，这里引用体是,这个引用只是后边第一个被转义吧，也就是a。\n",
    "\n",
    "; 赋值的形式是(set! <var> <value>)\n",
    "(define (assignment? exp) (tagged-list? exp 'set!))  \n",
    "(define (assignment-variable exp) (cadr exp))  ; 是取得上边的<var>\n",
    "(define (assignment-value exp) (caddr exp))    ; 去取得上边的<value>\n",
    "\n",
    "; 定义的基本形式\n",
    "; (define <var> <value>)\n",
    "; 或者\n",
    "; (define (⟨var⟩ ⟨parameter1⟩ . . . ⟨parametern⟩)\n",
    ";      ⟨body⟩)\n",
    "; 后一种是如下的包装\n",
    ";(define ⟨var⟩\n",
    ";  (lambda (⟨parameter1⟩ . . . ⟨parametern⟩)\n",
    ";    ⟨body⟩))\n",
    "(define (definition? exp) (tagged-list? exp 'define))  ; 是否是定义。\n",
    "(define (definition-variable exp)  ; 取得名字\n",
    "  (if (symbol? (cadr exp))         ; 如果是符号\n",
    "      (cadr exp)                   ; 就返回这个符号，格式是这种 (define <var> <value>)\n",
    "      (caadr exp)))                ; 那么格式就是这种 (define (⟨var⟩ ⟨parameter1⟩ . . . ⟨parametern⟩)  <body>) \n",
    "(define (definition-value exp)     ; 取得值，定义的值或者过程体\n",
    "  (if (symbol? (cadr exp))         ; 同样需要判断是否是符号，毕竟2种格式呢。\n",
    "      (caddr exp)\n",
    "      (make-lambda (cdadr exp)     ; formal parameters，如果是过程，这个会生成匿名函数。\n",
    "                   (cddr exp))))   ; body\n",
    "\n",
    "; lambda 表达式是如下的表 格式是 (lambda (参数表) (函数体))\n",
    "(define (lambda? exp) (tagged-list? exp 'lambda))  ; 判断第一个是否是lambda\n",
    "(define (lambda-parameters exp) (cadr exp))        ; 第二个是参数表\n",
    "(define (lambda-body exp) (cddr exp))              ; 第三个是函数体\n",
    "; lambda的构造过程，也就是构造这个格式('lambda (参数表) (函数体))\n",
    "(define (make-lambda parameters body)\n",
    "  (cons 'lambda (cons parameters body)))\n",
    "\n",
    "; if相关的，格式是(if (条件) (真的时候执行) (假的时候执行))\n",
    "(define (if? exp) (tagged-list? exp 'if))  ; \n",
    "(define (if-predicate exp) (cadr exp))     ; if 中的条件\n",
    "(define (if-consequent exp) (caddr exp))   ; if为真执行的\n",
    "(define (if-alternative exp)               ; if为假执行的\n",
    "  (if (not (null? (cdddr exp)))            ; 这部分可以为空的,这里判断一下。\n",
    "      (cadddr exp)\n",
    "      'false))\n",
    "; if的构造过程,\n",
    "(define (make-if predicate consequent alternative)\n",
    "  (list 'if predicate consequent alternative)) ; 这行可以对比上边的if-predicate等,就可以看出来分别取得什么了。\n",
    "\n",
    "; begin表达式\n",
    "(define (begin? exp) (tagged-list? exp 'begin))\n",
    "(define (begin-actions exp) (cdr exp))          ; 所有需要执行的表达式。这个是复数。\n",
    "(define (last-exp? seq) (null? (cdr seq)))      ; 判断是否是最后一个。\n",
    "(define (first-exp seq) (car seq))              ; 第一个，就是car\n",
    "(define (rest-exps seq) (cdr seq))              ; 其他所有的，是cdr\n",
    "; 将一个序列转化为begin表达式\n",
    "(define (sequence->exp seq)                     ; 将一个序列转化为表达式，\n",
    "  (cond ((null? seq) seq)                       ; 如果为空，就他自己\n",
    "        ((last-exp? seq) (first-exp seq))       ; 如果只有一个，也是自己，\n",
    "        (else (make-begin seq))))               ; 其他情况，转成begin表达式。\n",
    "(define (make-begin seq) (cons 'begin seq))     ; 这个是转化为begin表达式。\n",
    "; 过程应用，car是运算符，cdr是运算对象的表。\n",
    "(define (application? exp) (pair? exp))  ; （是过程应用\n",
    "(define (operator exp) (car exp))        ; 取得函数名\n",
    "(define (operands exp) (cdr exp))        ; 取得函数体\n",
    "(define (no-operands? ops) (null? ops))  ; 没有运算符？\n",
    "(define (first-operand ops) (car ops))   ; 第一个操作数\n",
    "(define (rest-operands ops) (cdr ops))   ; 其他操作数\n",
    "\n",
    "; 将cond规约成if，cond可以用嵌套的if表达。\n",
    "(define (cond? exp) (tagged-list? exp 'cond))    ; 判断\n",
    "(define (cond-clauses exp) (cdr exp))            ; cond过程体，cdr，应该是'cond后所有的\n",
    "(define (cond-else-clause? clause)               ; cond最后不是有个else吗？\n",
    "  (eq? (cond-predicate clause) 'else))           ; 这里判断是否是else的\n",
    "(define (cond-predicate clause) (car clause))    ; 第一个是判断的\n",
    "(define (cond-actions clause) (cdr clause))      ; 其他是执行的\n",
    "(define (cond->if exp) (expand-clauses (cond-clauses exp)))  ; 是所有的过程体\n",
    "\n",
    "(define (expand-clauses clauses)\n",
    "  (if (null? clauses)                 ; 首先判断是否结束吧。\n",
    "      'false ; no else clause\n",
    "      (let ((first (car clauses))     ; 先处理第一个，\n",
    "            (rest (cdr clauses)))     ; 其他的递归。\n",
    "        (if (cond-else-clause? first) ; 判断是否是else，因为else特殊。\n",
    "            (if (null? rest)          ; else是最后一个啦。\n",
    "                (sequence->exp (cond-actions first))       ; 将其他执行的转变为begin表达式。\n",
    "                (error \"ELSE clause isn't last: COND->IF\"  ; else不是最后一个，\n",
    "                       clauses))\n",
    "            (make-if (cond-predicate first)                ; 构造if语句，这里是条件。\n",
    "                     (sequence->exp (cond-actions first))  ; 为真的时候执行\n",
    "                     (expand-clauses rest))))))            ; 为假的时候执行，这里用递归，实现的是嵌套if\n",
    "\n",
    "; 求值器的数据结构。\n",
    "; 谓词的检测\n",
    "(define (true? x) (not (eq? x false)))\n",
    "(define (false? x) (eq? x false))\n",
    "; 过程的表示\n",
    "; 是否是一个基本过程。\n",
    "(define (primitive-procedure? proc)\n",
    "  (tagged-list? proc 'primitive))\n",
    "\n",
    "(define (primitive-implementation proc) (cadr proc))\n",
    "; 将给定的过程应用于表args里的参数值，并返回这一应用的结果。\n",
    "; 如下的这个是简单过程。\n",
    "(define (apply-primitive-procedure proc args) \n",
    "  (apply-in-underlying-scheme\n",
    "   (primitive-implementation proc) args))\n",
    "; 复合过程的构造函数\n",
    "(define (make-procedure parameters body env)\n",
    "  (list 'procedure parameters body env))       ; 复合过程的格式是这个。\n",
    "(define (compound-procedure? p)                ; 判断是否为复合过程。\n",
    "  (tagged-list? p 'procedure))\n",
    "; 如下分别取得复合过程的3个部分。\n",
    "(define (procedure-parameters p) (cadr p))     ; 取得参数表\n",
    "(define (procedure-body p) (caddr p))          ; 取得函数体\n",
    "(define (procedure-environment p) (cadddr p))  ; 取得环境\n",
    "\n",
    "\n",
    "; 对环境的操作\n",
    "; 阵对环境的基本操作\n",
    "; 环境表示为一个框架的表,一个环境的外围环境就是这个表的cdr,空环境则直接用空表表示。\n",
    "(define (enclosing-environment env) (cdr env))  ; 外围环境\n",
    "(define (first-frame env) (car env))            ; 这个环境的第一个框架。\n",
    "(define the-empty-environment '())              ; 空环境。\n",
    "; 在环境里的每个框架一对表形成的序对，\n",
    "; 一个是这个框架种所有元素的表，另一个是它们的约束值的表，也就是变量名和相应的变量值在两个表啦。\n",
    "(define (make-frame variables values) ; 框架的构造函数\n",
    "  (cons variables values))            ; 其实就是连接两个表，一个是变量名，一个是变量值。\n",
    "(define (frame-variables frame) (car frame))   ; 取得变量名\n",
    "(define (frame-values frame) (cdr frame))      ; 取得变量值\n",
    "(define (add-binding-to-frame! var val frame)  ; 添加新值\n",
    "  (set-car! frame (cons var (car frame)))      ; 就是在外边再包裹一层，然后修改地址。\n",
    "  (set-cdr! frame (cons val (cdr frame))))     ; 同理。\n",
    "\n",
    "; 返回符号在环境env里的约束值，\n",
    "; 先扫描第一个框架里的变量名，如果找到了就返回\n",
    "; 如果没找到就到外围环境中去找。\n",
    "; 如果遇到空环境，就发出错误信号。\n",
    "(define (lookup-variable-value var env)\n",
    "  (define (env-loop env)         ; 定义一个内部函数\n",
    "    (define (scan vars vals)     ; 内部函数,扫描\n",
    "      (cond ((null? vars)        ; 如果找到最后也没找到\n",
    "             (env-loop (enclosing-environment env))) ; 这里递归调用，从外围环境中继续找。\n",
    "            ((eq? var (car vars)) (car vals))        ; 这里表示找到了\n",
    "            (else (scan (cdr vars) (cdr vals)))))    ; 一个框架里可能有很多个变量名，这个是继续递归余下的。\n",
    "    (if (eq? env the-empty-environment)      ; 如果环境为空，\n",
    "        (error \"Unbound variable\" var)       ; 这里表示已经找到最顶层,但没找到\n",
    "        (let ((frame (first-frame env)))     ; 首先在第一个环境中去找。\n",
    "          (scan (frame-variables frame)      ; 两个参数，一个变量名\n",
    "                (frame-values frame)))))     ; 一个变量值。\n",
    "  (env-loop env))\n",
    "\n",
    "; 返回一个新环境，这个环境包含了一个新的框架，所有位域vars中的元素约束到表vals里对应的元素，而其外围环境是base-env\n",
    "(define (extend-environment vars vals base-env)\n",
    "  (if (= (length vars) (length vals))           ; 首先检查名称和值的个数是否相等。\n",
    "      (cons (make-frame vars vals) base-env)    ; 组件一个框架\n",
    "      (if (< (length vars) (length vals))       ; 不相等，就2种情况啦。\n",
    "          (error \"Too many arguments supplied\" vars vals)\n",
    "          (error \"Too few arguments supplied\" vars vals))))\n",
    ";在环境env里的第一个框架加入一个新约束，它关联其变量var和值val\n",
    "(define (define-variable! var val env)\n",
    "  (let ((frame (first-frame env)))   ; 第一个框架\n",
    "    (define (scan vars vals)         ; 这个扫描是扫描是否已有这个变量名，\n",
    "      (cond ((null? vars)            ; 判断是否扫描到最后了                  \n",
    "             (add-binding-to-frame! var val frame))    ; 这里表示没有，然后就新建啦。\n",
    "            ((eq? var (car vars)) (set-car! vals val)) ; 这里表示已经有了，修改值啦\n",
    "            (else (scan (cdr vars) (cdr vals)))))      ; 递归处理余下的。\n",
    "    (scan (frame-variables frame) (frame-values frame))))\n",
    "\n",
    "; 修改变量在环境里的约束。\n",
    "(define (set-variable-value! var val env)     ; 参数依次为变量名,变量值,环境。\n",
    "  (define (env-loop env)\n",
    "    (define (scan vars vals)\n",
    "      (cond ((null? vars)                                ; 判断是否到最后\n",
    "             (env-loop (enclosing-environment env)))     ; 这层找完了，递归处理上一层。\n",
    "            ((eq? var (car vars)) (set-car! vals val))   ; 如果找到了,就修改成新的值。\n",
    "            (else (scan (cdr vars) (cdr vals)))))        ; 递归处理余下的\n",
    "    (if (eq? env the-empty-environment)         ; 首先判断是否有环境吧。\n",
    "        (error \"Unbound variable: SET!\" var)    ; 环境为空返回这个。\n",
    "        (let ((frame (first-frame env)))        ; 第一个框架\n",
    "          (scan (frame-variables frame)         ; 变量名\n",
    "                (frame-values frame)))))        ; \n",
    "  (env-loop env))\n",
    "\n",
    "\n",
    "; 一个基本过程对象表\n",
    "(define primitive-procedures\n",
    "  (list (list 'car car)\n",
    "        (list 'cdr cdr)\n",
    "        (list 'cons cons)\n",
    "        (list 'null? null?)\n",
    "       ; ⟨more primitives⟩\n",
    "        ))\n",
    "; 取得基本过程名称\n",
    "(define (primitive-procedure-names)\n",
    "  (map car primitive-procedures))\n",
    "; 取得基本过程的实现，前面加 'primitive ，以便eval识别。\n",
    "(define (primitive-procedure-objects)\n",
    "  (map (lambda (proc) (list 'primitive (cadr proc)))  ; 基本过程以'primitive开头啦。\n",
    "       primitive-procedures))\n",
    "\n",
    "(define (setup-environment)\n",
    "  (let ((initial-env      ; 初始环境\n",
    "         (extend-environment (primitive-procedure-names)   ; 变量名\n",
    "                             (primitive-procedure-objects) ; 变量值\n",
    "                             the-empty-environment)))      ; 上一层环境是空环境啦。\n",
    "    (define-variable! 'true true initial-env)    ; 添加新约束\n",
    "    (define-variable! 'false false initial-env)\n",
    "    initial-env))  ; 返回构造的初始环境。\n",
    "(define the-global-environment (setup-environment))     ; 初始环境为全局环境。\n",
    "\n",
    "(define input-prompt \";;; M-Eval input:\")\n",
    "(define output-prompt \";;; M-Eval value:\")\n",
    "; 模拟读入、求值、打印的循环。\n",
    "(define (driver-loop)\n",
    "  (prompt-for-input input-prompt)                       ; 输入前缀\n",
    "  (let ((input (read)))                                 ; 输入\n",
    "    (let ((output (eval input the-global-environment))) ; 求值\n",
    "      (announce-output output-prompt)                   ; 打印前缀\n",
    "      (user-print output)))                             ; 打印输出。\n",
    "  (driver-loop))\n",
    "\n",
    "(define (prompt-for-input string)  ; 输入字符串。\n",
    "  (newline) (newline) (display string) (newline))\n",
    "(define (announce-output string)   ; 打印字符串。\n",
    "  (newline) (display string) (newline))\n",
    "\n",
    "; 打印\n",
    "(define (user-print object)\n",
    "  (if (compound-procedure? object)                  ; 首先判断是否为复合过程。\n",
    "      (display (list 'compound-procedure            ; 复合过程标志头\n",
    "                     (procedure-parameters object)  ; 参数\n",
    "                     (procedure-body object)        ; 函数体\n",
    "                     '<procedure-env>))             ; 这个是环境。\n",
    "      (display object)))\n",
    "\n",
    "\n",
    "(define apply-in-underlying-scheme apply)\n",
    "\n",
    "; 这里开始执行吧。\n",
    "(driver-loop)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Racket",
   "language": "racket",
   "name": "racket"
  },
  "language_info": {
   "codemirror_mode": "scheme",
   "file_extension": ".rkt",
   "mimetype": "text/x-racket",
   "name": "Racket",
   "pygments_lexer": "racket",
   "version": "7.7.0.901"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
